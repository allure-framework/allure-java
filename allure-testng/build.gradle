description = 'Allure TestNG'

apply from: "${gradleScriptDir}/maven-publish.gradle"
apply from: "${gradleScriptDir}/bintray.gradle"
apply plugin: 'maven'

configurations {
    agent
}

dependencies {
    agent('org.aspectj:aspectjweaver')

    compile project(':allure-descriptions-javadoc')
    compile project(':allure-java-commons')
    compile('org.testng:testng')

    testCompile project(':allure-java-commons-test')
    testCompile('com.google.inject:guice')
    testCompile('org.assertj:assertj-core')
    testCompile('org.mockito:mockito-core')
    testCompile('org.slf4j:slf4j-simple')
}

jar {
    from('src/main/services') {
        into 'META-INF/services'
    }
}

jar {
    manifest {
        attributes(
                'Automatic-Module-Name': 'io.qameta.allure.testng'
        )
    }
}

test {
    useTestNG {
        suites 'src/test/resources/testng.xml'
    }

    systemProperty 'org.slf4j.simpleLogger.defaultLogLevel', 'debug'
    exclude 'io/qameta/allure/testng/samples/**'

    doFirst {
        jvmArgs "-javaagent:${configurations.agent.singleFile}"
    }
}

task spiOffJar(type: Jar, dependsOn: classes) {
    classifier = 'spi-off'
    from sourceSets.main.output
}

configurations {
    spiOff.extendsFrom(compile)
}

artifacts {
    spiOff spiOffJar
    archives spiOffJar
}
