description = 'Allure AssertJ'

apply from: "${gradleScriptDir}/maven-publish.gradle"
apply from: "${gradleScriptDir}/bintray.gradle"
apply plugin: 'maven'
apply plugin: 'org.junit.platform.gradle.plugin'

configurations {
    agent
}

dependencies {
    agent 'org.aspectj:aspectjweaver'

    compile project(':allure-java-commons')
    compile 'org.assertj:assertj-core'

    testCompile 'org.slf4j:slf4j-simple'
    testCompile('org.junit.jupiter:junit-jupiter-api')
    testRuntime('org.junit.jupiter:junit-jupiter-engine')
    testCompile project(':allure-junit-platform')
    testCompile project(':allure-java-commons-test')
}

junitPlatform {
    filters {
        engines {
            include 'junit-jupiter'
        }
        includeClassNamePattern '.*Test'
    }
    enableStandardTestTask true
}

tasks.withType(JavaExec) {
    if (it.name == 'junitPlatformTest') {
        systemProperty 'org.slf4j.simpleLogger.defaultLogLevel', 'debug'
        systemProperty 'allure.model.indentOutput', true
        systemProperty 'allure.results.directory', 'build/allure-results'
        doFirst {
            jvmArgs "-javaagent:${configurations.agent.singleFile}"
        }
    }
}

