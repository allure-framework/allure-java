apply plugin: 'maven'
apply plugin: 'maven-publish'

install {
    repositories.mavenInstaller {
        customizePom(pom, project)
    }
}

publishing {
    repositories {
        maven {
            name = "OSSRH"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

def customizePom(pom, gradleProject) {
    pom.whenConfigured { generatedPom ->
        // eliminate testng-scoped dependencies (no need in maven central poms)
        generatedPom.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // sort to make pom dependencies order consistent to ease comparison of older poms
        generatedPom.dependencies = generatedPom.dependencies.sort { dep ->
            "$dep.scope:$dep.groupId:$dep.artifactId"
        }

        // add all items necessary for maven central publication
        generatedPom.project {
            name = gradleProject.description
            description = gradleProject.description
            url = "https://github.com/allure-framework/allure-java"
            organization {
                name = "Qameta IO"
                url = "https://qameta.io"
            }
            licenses {
                license {
                    name = 'The Apache Software License, Version 2.0'
                    url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution = 'repo'
                }
            }
            scm {
                url = 'https://github.com/allure-framework/allure-java'
                connection = 'scm:git:git://github.com/allure-framework/allure-java'
                developerConnection = 'scm:git:git://github.com/allure-framework/allure-java'
            }
            developers {
                developer {
                    id = 'baev'
                    name = 'Dmitry Baev'
                    email = 'baev@qameta.io'
                }
            }
            issueManagement {
                system = 'Github Issues'
                url = 'https://github.com/allure-framework/allure-java/issues'
            }
        }
    }
}
